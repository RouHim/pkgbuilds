# shellcheck disable=SC2034,SC2154,SC2164
pkgname=('cadvisor')
_srcname='cadvisor'
pkgdesc='Analyzes resource usage and performance characteristics of running containers'
pkgver='0.34.0'
_commit='24a6a52fceb2e1c5a319b97bfc6bebf8f5b816cf'
pkgrel='1'
arch=('i686' 'x86_64')
url="https://github.com/google/${_srcname}"
license=('Apache')

makedepends=('git' 'make' 'go' 'godep')

source=("${_srcname}::git+${url}.git#commit=${_commit}")
sha512sums=('SKIP')

_url_go="${url#*//}"

prepare() {
    cd "${srcdir}"
    export GOPATH="${srcdir}/gopath"

    mkdir --parents "${GOPATH}/src/${_url_go}"
    rm --recursive "${GOPATH}/src/${_url_go}"
    mv "${_srcname}" "${GOPATH}/src/${_url_go}"

    cd "${GOPATH}/src/${_url_go}"
    git remote set-url origin "${url}"
}

build() {
    cd "${srcdir}"
    export GOPATH="${srcdir}/gopath"

    cd "${GOPATH}/src/${_url_go}"
    make GO_FLAGS='-v' build
}

package() {
    export GOPATH="${srcdir}/gopath"
    cd "${GOPATH}/src/${_url_go}"

    install -D --mode='755' cadvisor "${pkgdir}/usr/bin/cadvisor"
}
