# shellcheck disable=SC2034,SC2154,SC2164
pkgname=('prometheus')
_srcname='prometheus'
pkgdesc='Monitoring system and time series database'
pkgver='2.14.0'
_commit='edeb7a44cbf745f1d8be4ea6f215e79e651bfe19'
pkgrel='1'
arch=('i686' 'x86_64')
url="https://github.com/prometheus/${_srcname}"
license=('Apache')

depends=('glibc')
makedepends=('git' 'go-pie' 'yarn')

source=("${_srcname}::git+${url}.git#commit=${_commit}")
sha512sums=('SKIP')

_url_go="${url#*//}"

prepare() {
    cd "${srcdir}"
    export GOPATH="${srcdir}/gopath"

    mkdir --parents "${GOPATH}/src/${_url_go}"
    rm --recursive "${GOPATH}/src/${_url_go}"
    mv "${_srcname}" "${GOPATH}/src/${_url_go}"

    cd "${GOPATH}/src/${_url_go}"
    git remote set-url origin "${url}"
}

build() {
    cd "${srcdir}"
    export GOPATH="${srcdir}/gopath"

    cd "${GOPATH}/src/${_url_go}"
    make build
}

package() {
    cd "${srcdir}"
    export GOPATH="${srcdir}/gopath"

    install --directory "${pkgdir}/"{"etc/${_srcname}",usr/bin,"var/lib/${_srcname}"}
    cp "${GOPATH}/src/${_url_go}/"{prometheus,promtool} "${pkgdir}/usr/bin/"

    cd "${GOPATH}/src/${_url_go}"
    cp config/testdata/conf.good.yml "${pkgdir}/etc/${_srcname}/config.yml"

    chmod --recursive 'u=rwX,g=rX,o=rX' "${pkgdir}"
}
