# shellcheck disable=SC2034,SC2154,SC2164
pkgname=('prometheus-exporter-node')
_srcname='node_exporter'
pkgdesc='Prometheus exporter for machine metrics'
pkgver='0.17.0'
_commit='f6f6194a436b9a63d0439abc585c76b19a206b21'
pkgrel='1'
arch=('i686' 'x86_64')
url="https://github.com/prometheus/${_srcname}"
license=('Apache')

makedepends=('git' 'go')
provides=("${pkgname[0]%-git}")

source=(
    "${_srcname}::git+${url}.git#commit=${_commit}"
    'prometheus-exporter-node.service'
)
sha512sums=(
    'SKIP'
    'SKIP'
)

install='install.sh'

_url_go="${url#*//}"

prepare() {
    cd "${srcdir}"
    export GOPATH="${srcdir}/gopath"

    mkdir --parents "${GOPATH}/src/${_url_go}"
    rm --recursive "${GOPATH}/src/${_url_go}"
    mv "${_srcname}" "${GOPATH}/src/${_url_go}"
}

build() {
    cd "${srcdir}"
    export GOPATH="${srcdir}/gopath"

    cd "${GOPATH}/src/${_url_go}"
    make build
}

package() {
    cd "${srcdir}"
    export GOPATH="${srcdir}/gopath"

    install -D "${GOPATH}/src/${_url_go}/node_exporter" "${pkgdir}/usr/bin/prometheus-exporter-node"
    install -D --mode='644' "${srcdir}/prometheus-exporter-node.service" \
        "${pkgdir}/usr/lib/systemd/system/prometheus-exporter-node.service"
}
