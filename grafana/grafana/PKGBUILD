# shellcheck disable=SC2034,SC2154,SC2164
pkgname=('grafana')
_srcname='grafana'
_pkgbasename="${pkgname[0]%-git}"
pkgdesc='Feature rich metrics dashboard and graph editor'
pkgver='6.5.0'
_commit='24a5d2fe8b3eb9111861b11f5b718da52717c6cd'
pkgrel='1'
arch=('i686' 'x86_64')
url="https://github.com/grafana/${_srcname}"
license=('Apache')

makedepends=('git' 'go' 'npm' 'python2' 'yarn')

source=(
    "${_srcname}::git+${url}.git"
    'package_json.patch'
)
sha512sums=(
    'SKIP'
    'SKIP'
)

_url_go="${url#*//}"

prepare() {
    cd "${srcdir}/${_srcname}"
    export GOPATH="${srcdir}/gopath"

    mkdir --parents "${GOPATH}/src/${_url_go}"
    rm --recursive "${GOPATH}/src/${_url_go}"
    ln --symbolic --relative --force "${srcdir}/${_srcname}" "${GOPATH}/src/${_url_go}"

    cd "${GOPATH}/src/${_url_go}"
    git remote set-url origin "${url}"
    go run build.go setup
    git checkout --quiet "${_commit}"
    git apply "${srcdir}/package_json.patch"

    npm install
}

build() {
    cd "${srcdir}/${_srcname}"
    export GOPATH="${srcdir}/gopath"

    cd "${GOPATH}/src/${_url_go}"
    go run build.go build

    yarn install
    npm run build release
}

package() {
    cd "${srcdir}/${_srcname}"
    export GOPATH="${srcdir}/gopath"

    install --directory "${pkgdir}/"{"etc/${_pkgbasename}",usr/bin,"usr/share/${_pkgbasename}","var/lib/${_pkgbasename}"}
    install --target-directory="${pkgdir}/usr/bin" bin/linux-amd64/grafana-cli bin/linux-amd64/grafana-server

    install -D --mode='644' conf/defaults.ini "${pkgdir}/usr/share/${_pkgbasename}/conf/defaults.ini"
    install -D --mode='644' conf/sample.ini "${pkgdir}/etc/${_pkgbasename}/grafana.ini"

    for dir in vendor public tools; do
        cp --recursive "${dir}" "${pkgdir}/usr/share/${_pkgbasename}/"
    done

    chmod --recursive 'u=rwX,g=rX,o=rX' "${pkgdir}"
}
