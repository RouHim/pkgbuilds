# shellcheck disable=SC2034,SC2154,SC2164
pkgname=('parity')
_srcname='parity-ethereum'
pkgdesc='Fast, light, robust Ethereum implementation'
pkgver='2.5.5'
_commit='3ebc7697578269dee29fea380407569882a70826'
pkgrel='1'
arch=('i686' 'x86_64')
url="https://github.com/paritytech/${_srcname}"
license=('GPL3')

depends=('openssl' 'libsystemd')
makedepends=('git' 'rust' 'cargo' 'systemd' 'gcc' 'pkg-config' 'cmake')

source=("${_srcname}::git+${url}.git#commit=${_commit}")
sha512sums=('SKIP')

build() {
    cd "${srcdir}/${_srcname}"

    export CARGO_HOME="${srcdir}/cargo"
    cargo build --release --features final
}

package() {
    cd "${srcdir}/${_srcname}"

    install -D --mode=755 target/release/parity "${pkgdir}/usr/bin/parity"
    install -D --mode=644 LICENSE "${pkgdir}/usr/share/licences/${pkgname[0]%-git}/LICENSE"
}
