# shellcheck disable=SC2034,SC2154,SC2164
pkgname=('prysm')
_srcname='prysm'
pkgdesc='Go implementation of the Ethereum 2.0 blockchain'
pkgver='1.3.2'
_commit='29d1959b81921946f620b8fc745be660c8185d39'
pkgrel='1'
arch=('x86_64')
url="https://github.com/prysmaticlabs/${_srcname}"
license=('GPL3')

makedepends=('git' 'go')
provides=("${pkgname[0]%-git}")
conflicts=("${pkgname[0]%-git}")

source=("${_srcname}::git+${url}.git#commit=${_commit}")
sha512sums=('SKIP')

build() {
    cd "${srcdir}/${_srcname}"

    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"

    mkdir output
    go build -o output -tags=blst_enabled -v ./...
}

package() {
    cd "${srcdir}/${_srcname}"

    cd output
    for target in *; do
        install -D --mode=755 "${target}" "${pkgdir}/usr/bin/prysm-${target}"
    done
}
