# shellcheck disable=SC2034,SC2154,SC2164
pkgname=('openethereum')
_srcname='openethereum'
pkgdesc='Fast, light, robust Ethereum implementation'
pkgver='3.1.0'
_commit='06fc61d7c5ce29877348014db85cfeabe5b9c807'
pkgrel='1'
arch=('i686' 'x86_64')
url="https://github.com/openethereum/${_srcname}"
license=('GPL3')

depends=('gcc-libs')
makedepends=('git' 'rust' 'clang' 'cmake')

source=("${_srcname}::git+${url}.git#commit=${_commit}")
sha512sums=('SKIP')

build() {
    cd "${srcdir}/${_srcname}"

    export CARGO_HOME="${srcdir}/cargo"
    cargo build --release --features final
}

package() {
    cd "${srcdir}/${_srcname}"

    install -D --mode=755 target/release/openethereum "${pkgdir}/usr/bin/openethereum"
    install -D --mode=644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname[0]%-git}/LICENSE"
}
